{-# OPTIONS --safe --without-K #-}

import Type
module SMLC.Term.Conversion (ğ•¤ : Type.Ty) where

open import SMLC.Term.Base ğ•¤
open import SMLC.Term.Properties ğ•¤

open import Relation.Binary
  using (Setoid ; IsEquivalence)
open import Relation.Binary.Construct.Closure.Equivalence
  using (setoid)
import Relation.Binary.Construct.Closure.Equivalence.Properties
  as EquivalenceProperties

open import Relation.Binary.PropositionalEquality
  using    (_â‰¡_ ; cong ; congâ‚‚)
  renaming (refl to â‰¡-refl ; sym to â‰¡-sym ; trans to â‰¡-trans)

import Relation.Binary.Reasoning.Setoid as EqReasoning

data _â‰ˆ_ : Tm Î“ a â†’ Tm Î“ a â†’ Set where

  red-fun : (t : Tm (Î“ `, a) b) (u : Tm Î“ a)
    â†’ app (lam t) u â‰ˆ substTm (ids `, u) t

  exp-fun : (t : Tm Î“ (a â‡’ b))
    â†’ t â‰ˆ lam (app (wkTm freshWk t) (var zero))
    
  red-dia : (t : Tm Î“ a)  (u : Tm (Î“ `, a) (ğ• b))
    â†’ letin (return t) u â‰ˆ substTm (ids `, t) u

  exp-dia : (t : Tm Î“ (ğ• a))
    â†’ t â‰ˆ letin t (return (var zero))

  ass-dia : (t : Tm Î“ (ğ• a)) (u : Tm (Î“ `, a) (ğ• b)) (u' : Tm (Î“ `, b) (ğ• c))
    â†’ letin (letin t u) u' â‰ˆ letin t (letin u (wkTm (keep freshWk) u'))

  alg-gp : (t : Tm Î“ (ğ• a))
    â†’ t â‰ˆ get (put (var zero) (wkTm freshWk t))
  
  alg-pp : (t : Tm Î“ ğ•¤) (t' : Tm Î“ ğ•¤) (u : Tm Î“ (ğ• a))
    â†’ put t (put t' u) â‰ˆ put t' u

  alg-pg : (t : Tm Î“ ğ•¤) (u : Tm (Î“ `,  ğ•¤) (ğ• a))
    â†’ put t (get u) â‰ˆ put t (substTm (ids `, t) u)

  alg-get : (t : Tm (Î“ `,  ğ•¤) (ğ• a)) (u : Tm (Î“ `, a) (ğ• b))
    â†’ letin (get t) u â‰ˆ get (letin t (wkTm (keep freshWk) u))

  alg-put : (t : Tm Î“ ğ•¤) (u : Tm Î“ (ğ• a)) (u' : Tm (Î“ `, a) (ğ• b))
    â†’ letin (put t u) u' â‰ˆ put t (letin u u')

  cong-lam : {t t' : Tm (Î“ `, a) b}
    â†’ t â‰ˆ t'
    â†’ lam t â‰ˆ lam t'

  cong-app1 : {t t' : Tm Î“ (a â‡’ b)} {u : Tm Î“ a}
    â†’ t â‰ˆ t'
    â†’ app t u â‰ˆ app t' u

  cong-app2 : {t : Tm Î“ (a â‡’ b)} {u u' : Tm Î“ a}
    â†’ u â‰ˆ u'
    â†’ app t u â‰ˆ app t u'

  cong-return : {t t' : Tm Î“ a}
    â†’ t â‰ˆ t'
    â†’ return t â‰ˆ return t'
    
  cong-letin1 : {t t' : Tm Î“ (ğ• a)} {u : Tm (Î“ `, a) (ğ• b)}
    â†’ t â‰ˆ t'
    â†’ letin t u â‰ˆ letin t' u

  cong-letin2 : {t : Tm Î“ (ğ• a)} {u u' : Tm (Î“ `, a) (ğ• b)}
    â†’ u â‰ˆ u'
    â†’ letin t u â‰ˆ letin t u'

  cong-get : {t t' : Tm (Î“ `, ğ•¤) (ğ• a)} 
    â†’ t â‰ˆ t'
    â†’ get t â‰ˆ get t'
    
  cong-put : {t t' : Tm Î“ ğ•¤} {u : Tm Î“ (ğ• a)}
    â†’ t â‰ˆ t'
    â†’ u â‰ˆ u'
    â†’ put t u â‰ˆ put t' u
    
  â‰ˆ-refl : {t : Tm Î“ a}
    â†’ t â‰ˆ t

  â‰ˆ-sym : {t u : Tm Î“ a}
    â†’ t â‰ˆ u â†’ u â‰ˆ t

  â‰ˆ-trans : {t u u' : Tm Î“ a}
    â†’ t â‰ˆ u â†’ u â‰ˆ u' â†’ t â‰ˆ u'

â‰ˆ-is-equiv : IsEquivalence (_â‰ˆ_ {Î“} {a})
â‰ˆ-is-equiv = record
    { refl  = â‰ˆ-refl
    ; sym   = â‰ˆ-sym
    ; trans = â‰ˆ-trans
    }

Tm-setoid : (Î“ : Ctx) â†’ (a : Ty) â†’ Setoid _ _
Tm-setoid Î“ a = record
  { Carrier       = Tm Î“ a
  ; _â‰ˆ_           = _â‰ˆ_
  ; isEquivalence = â‰ˆ-is-equiv
  }

â‰¡-to-â‰ˆ : âˆ€ {t u : Tm Î“ a} â†’ t â‰¡ u â†’ t â‰ˆ u
â‰¡-to-â‰ˆ â‰¡-refl = â‰ˆ-refl

cong-appâ‰ˆâ‰¡ : âˆ€ (tâ‰ˆt' : t â‰ˆ t') (uâ‰¡u' : u â‰¡ u') â†’ app t u â‰ˆ app t' u
cong-appâ‰ˆâ‰¡ tâ‰ˆt' â‰¡-refl = cong-app1 tâ‰ˆt'

cong-app1â‰ˆ : âˆ€ (tâ‰ˆt' : t â‰ˆ t') â†’ app t u â‰ˆ app t' u
cong-app1â‰ˆ tâ‰ˆt' = cong-appâ‰ˆâ‰¡ tâ‰ˆt' â‰¡-refl

cong-appâ‰¡â‰ˆ : âˆ€ (tâ‰¡t' : t â‰¡ t') (uâ‰ˆu' : u â‰ˆ u') â†’ app t u â‰ˆ app t' u'
cong-appâ‰¡â‰ˆ â‰¡-refl uâ‰ˆu' = cong-app2 uâ‰ˆu'

cong-app2â‰ˆ : âˆ€ (uâ‰ˆu' : u â‰ˆ u') â†’ app t u â‰ˆ app t u'
cong-app2â‰ˆ uâ‰ˆu' = cong-appâ‰¡â‰ˆ â‰¡-refl uâ‰ˆu'

cong-appâ‰ˆ : âˆ€ (tâ‰ˆt' : t â‰ˆ t') (uâ‰ˆu' : u â‰ˆ u') â†’ app t u â‰ˆ app t' u'
cong-appâ‰ˆ tâ‰ˆt' uâ‰ˆu' = â‰ˆ-trans (cong-app1â‰ˆ tâ‰ˆt') (cong-app2â‰ˆ uâ‰ˆu')

cong-letin : {t t' : Tm Î“ (ğ• a)} {u : Tm (Î“ `, a) (ğ• b)}
    â†’ t â‰ˆ t' â†’ u â‰ˆ u' â†’ letin t u â‰ˆ letin t' u'
cong-letin tâ‰ˆt' uâ‰ˆu' = â‰ˆ-trans (cong-letin1 tâ‰ˆt') (cong-letin2 uâ‰ˆu')


--
-- Derived equations
--

--
-- Derived lemmas for proving the fundamental theorem
--

open AdhocLemmas

wkTm-pres-â‰ˆ : (w : Î“ âŠ† Î“') {t t' : Tm Î“ a} â†’ t â‰ˆ t' â†’ wkTm w t â‰ˆ wkTm w t'
wkTm-pres-â‰ˆ w (red-fun t u)         = â‰ˆ-trans (red-fun _ _) (â‰¡-to-â‰ˆ (red-fun-crunch-lemma w u t))
wkTm-pres-â‰ˆ w (exp-fun _)           = â‰ˆ-trans (exp-fun _) (â‰¡-to-â‰ˆ (cong lam (congâ‚‚ app keepFreshLemma â‰¡-refl)))
wkTm-pres-â‰ˆ w (red-dia t u)         = â‰ˆ-trans (red-dia _ _) (â‰¡-to-â‰ˆ (red-fun-crunch-lemma w t u))
wkTm-pres-â‰ˆ w (ass-dia t u u')      = â‰ˆ-trans (ass-dia _ _ _) (cong-letin2 (cong-letin2 (â‰¡-to-â‰ˆ (ass-dia-crunch-lemma w u'))))
wkTm-pres-â‰ˆ w (exp-dia _)           = exp-dia (wkTm w _)
wkTm-pres-â‰ˆ w (cong-lam tâ‰ˆt')       = cong-lam (wkTm-pres-â‰ˆ (_âŠ†_.keep w) tâ‰ˆt')
wkTm-pres-â‰ˆ w (cong-app1 tâ‰ˆt')      = cong-app1 (wkTm-pres-â‰ˆ w tâ‰ˆt')
wkTm-pres-â‰ˆ w (cong-app2 tâ‰ˆt')      = cong-app2 (wkTm-pres-â‰ˆ w tâ‰ˆt')
wkTm-pres-â‰ˆ w (cong-return tâ‰ˆt')    = cong-return (wkTm-pres-â‰ˆ w tâ‰ˆt')
wkTm-pres-â‰ˆ w (cong-letin1 tâ‰ˆt')    = cong-letin1 (wkTm-pres-â‰ˆ w tâ‰ˆt')
wkTm-pres-â‰ˆ w (cong-letin2 tâ‰ˆt')    = cong-letin2 (wkTm-pres-â‰ˆ (_âŠ†_.keep w) tâ‰ˆt')
wkTm-pres-â‰ˆ w â‰ˆ-refl                = â‰ˆ-refl
wkTm-pres-â‰ˆ w (â‰ˆ-sym tâ‰ˆt')          = â‰ˆ-sym (wkTm-pres-â‰ˆ w tâ‰ˆt')
wkTm-pres-â‰ˆ w (â‰ˆ-trans tâ‰ˆt' t'â‰ˆt'') = â‰ˆ-trans (wkTm-pres-â‰ˆ w tâ‰ˆt') (wkTm-pres-â‰ˆ w t'â‰ˆt'')
wkTm-pres-â‰ˆ w (alg-gp _)            = â‰ˆ-trans (alg-gp _) (cong-get (â‰¡-to-â‰ˆ (cong (put (var zero)) keepFreshLemma)))
wkTm-pres-â‰ˆ w (alg-pp t t' u)       = alg-pp (wkTm w t) (wkTm w t') (wkTm w u)
wkTm-pres-â‰ˆ w (alg-pg t u)          = â‰ˆ-trans (alg-pg _ _) (â‰¡-to-â‰ˆ (cong (put _) (red-fun-crunch-lemma w t u)))
wkTm-pres-â‰ˆ w (alg-get t u)         = â‰ˆ-trans (alg-get _ _) (â‰¡-to-â‰ˆ (cong get (cong (letin _) (ass-dia-crunch-lemma w u))))
wkTm-pres-â‰ˆ w (alg-put t u u')      = alg-put (wkTm w t) (wkTm w u) (wkTm (keep w) u')
wkTm-pres-â‰ˆ w (cong-get tâ‰ˆt')       = cong-get (wkTm-pres-â‰ˆ (keep w) tâ‰ˆt')
wkTm-pres-â‰ˆ w (cong-put tâ‰ˆt' tâ‰ˆt'') = cong-put (wkTm-pres-â‰ˆ w tâ‰ˆt') â‰ˆ-refl

red-fun-tr-lemma : (w : Î“ âŠ† Î“') (s : Sub Î“ Î”) (t : Tm (Î” `, a) b) (u : Tm Î“' a)
  â†’ app (wkTm w (substTm s (lam t))) u â‰ˆ substTm (wkSub w s `, u) t
red-fun-tr-lemma w s t u = let open EqReasoning (Tm-setoid _ _) in begin
    -- normalize
  app (lam (wkTm (keep w) (substTm (keeps s) t))) u
    â‰ˆâŸ¨ red-fun _ u  âŸ©
  substTm (ids `, u) (wkTm (keep w) (substTm (keeps s) t))
    â‰¡Ë˜âŸ¨ cong (substTm (ids `, u)) (substTm-nat t (keeps s) (keep w)) âŸ©
  substTm (ids `, u) (substTm (wkSub (keep w) (keeps s)) t)
    â‰¡Ë˜âŸ¨ substTm-pres-âˆ™s _ _ t âŸ©
  substTm (wkSub (keep w) (keeps s) âˆ™s (ids `, u)) t
    â‰¡Ë˜âŸ¨ cong (Î» s' â†’ substTm ((s' âˆ™s _) `, u) t) (wkSub-pres-âŠ†-trans _ _ _) âŸ©
  substTm ((wkSub (freshWk âˆ™ keep w) s âˆ™s (ids `, u)) `, u) t
    â‰¡âŸ¨ cong (Î» s' â†’ substTm (s' `, u) t) (cong (_âˆ™s _) (congâ‚‚ wkSub (cong drop (âŠ†-trans-unit-left w)) â‰¡-refl)) âŸ©
  substTm ((wkSub (drop w) s âˆ™s (ids `, u)) `, u) t
    â‰¡Ë˜âŸ¨ cong (Î» s' â†’ substTm (s' `, u) t) (assoc-wkSub-âˆ™s _ _ _) âŸ©
    -- normalize
  substTm ((s âˆ™s trimSub w ids) `, u) t
    â‰¡âŸ¨ cong (Î» s' â†’ substTm ((s âˆ™s s') `, u) t) (trimSub-unit-right w) âŸ©
  substTm ((s âˆ™s embWk w) `, u) t
    â‰¡Ë˜âŸ¨ cong (Î» s' â†’ substTm (s' `, u) t) (cong (s âˆ™s_) (wkSub-unit-right w)) âŸ©
  substTm ((s âˆ™s wkSub w ids) `, u) t
    â‰¡Ë˜âŸ¨ cong (Î» s' â†’ substTm (s' `, u) t) (assoc-âˆ™s-wkSub _ _ _) âŸ©
  substTm (wkSub w (s âˆ™s ids) `, u) t
    â‰¡âŸ¨ cong (Î» s' â†’ substTm (s' `, u) t) (cong (wkSub w) (âˆ™s-unit-right s)) âŸ©
  substTm (wkSub w s `, u) t âˆ   
